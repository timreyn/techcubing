apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.techcubing.android"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.14.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}

android {
    sourceSets {
        main {
            proto {
                srcDir '..'
                exclude '**/bazel-bin/**'
                exclude '**/bazel-genfiles/**'
                exclude '**/bazel-out/**'
                exclude '**/bazel-server/**'
                exclude '**/bazel-techcubing/**'
                exclude '**/build/**'
                exclude '**/third_party/**'
            }
        }
        main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'com.google.protobuf:protobuf-lite:3.0.1'
    api 'com.google.firebase:firebase-core:16.0.3'
    api 'com.google.firebase:firebase-ml-vision:17.0.0'
    api 'com.wonderkiln:camerakit:0.13.1'
    implementation 'io.grpc:grpc-android:1.14.0'
    implementation 'io.grpc:grpc-okhttp:1.14.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.14.0'
    implementation 'io.grpc:grpc-stub:1.14.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    api 'com.jakewharton:disklrucache:2.0.2'
    api 'javax.annotation:javax.annotation-api:1.3.2'
}

apply plugin: 'com.google.gms.google-services'